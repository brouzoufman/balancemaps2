actor TinierRocket: Rocket
{
    Radius 4
    Damage 0
    
    +HITTRACER
    Obituary "%o took a rocket to the face."
    
    States
    {
      Death:
        MISL B 0 bright ACS_NamedExecuteWithResult("BMark_DealDamage", 90, AAPTR_TRACER)
        MISL B 0 bright ACS_NamedExecuteWithResult("BMark_MarkFromProjectile", 175, AAPTR_TRACER)
        MISL B 8 bright A_Explode(128, 128, XF_HURTSOURCE | XF_NOTMISSILE)
        MISL C 6 bright
        MISL D 4 bright
        stop
    }
}

actor TinierDoomImpBall: DoomImpBall
{
    Radius 4
    SeeSound "imp/attack_quiet"
    DeathSound "imp/shotx_quiet"
    
    Damage (20)
}

actor QuietDoomImpBall: DoomImpBall
{
    SeeSound "imp/attack_quiet"
    DeathSound "imp/shotx_quiet"
    
    Damage (20)
}

actor HeavyDoomImpBall: DoomImpBall
{
    Scale 1.5
    Radius 8
    Height 12
    
    Damage (35)
}

actor CacodemonBall_Trap: CacodemonBall
{
    +HITTRACER
    
    SeeSound "imp/attack_quiet"
    DeathSound "imp/shotx_quiet"
    
    Damage 0
    
    States
    {
      Death:
        "----" A 0 ACS_NamedExecuteWithResult("BMark_DealDamage", 30, AAPTR_TRACER)
        "----" A 0 ACS_NamedExecuteWithResult("BMark_MarkFromProjectile", 175, AAPTR_TRACER)
        goto Super::Death
    }
}

actor CacodemonBall_Trap_NoKnock : CacodemonBall_Trap
{
	+NODAMAGETHRUST
	
	States
    {
      Death:
        "----" A 0 ACS_NamedExecuteWithResult("BMark_DealDamage", 10, AAPTR_TRACER)
        "----" A 0 ACS_NamedExecuteWithResult("BMark_MarkFromProjectile", 175, AAPTR_TRACER)
        goto Super::Death
    }
}

actor GravelordRevenant: Revenant
{
    Translation "80:111=168:191", "128:143=171:191", "152:159=176:191", "48:79=168:191", "144:151=180:191", "13:15=188:191", "160:167=172:188", "9:12=188:191", "236:239=191:191", "16:47=168:191", "208:223=168:191", "112:127=172:191"
    RenderStyle Add
    
    Health 600
    MinMissileChance 0
    PainChance 50
    +BRIGHT
    +LOOKALLAROUND
    +QUICKTORETALIATE
    
    Species "Funhouse"
    
    States
    {
      Missile:
        SKEL J 0 Bright A_FaceTarget
        SKEL J 10 Bright A_FaceTarget
        SKEL K 10 A_CustomMissile("GravelordRevenantTracer", 40)
        SKEL K 10 A_FaceTarget
        goto See
        
      Melee:
        SKEL G 0 A_FaceTarget
        SKEL G 4 A_SkelWhoosh
        SKEL H 4 A_FaceTarget
        SKEL I 4 A_SkelFist
        goto See
        
      Death:
        SKEL LM 7
        SKEL N 7 A_Scream
        SKEL O 7 A_NoBlocking
        SKEL P 7
        SKEL QQQQQQQQQQQQQQQQQQQQQQQQQ 1 A_FadeOut(0.04, 0)
        TNT1 A 140
        stop
    }
}

actor GravelordRevenantTracer: RevenantTracer
{
    +HITMASTER
    Damage (45)
    
    States
    {
      Death:
        "----" A 0 ACS_NamedExecuteWithResult("BMark_MarkFromProjectile", 175, AAPTR_MASTER)
        goto Super::Death
    }
}

actor BadRevenant : Revenant 29506
{
    //$Category "Balancemaps/Enemies"
    //$Title "Revenant (rude)"
    //$Color 12
    
	States
    {
      Missile:
        SKEL J 0 Bright A_FaceTarget
        SKEL J 10 Bright A_FaceTarget
        SKEL K 10 A_CustomMissile("BadRevenantTracer", 40)
        SKEL K 10 A_FaceTarget
        goto See
	}
}

actor BadRevenantTracer: RevenantTracer
{
	+MTHRUSPECIES
	Damage (25)
    
	States
	{
	Spawn:
		FATB AB 1 bright A_Tracer
		loop
	}
}

actor Driver : BaronOfHell 29507
{
    //$Category "Balancemaps/Enemies"
    //$Title "Baron of Hell (1 health)"
    //$Color 12
    
	Health 1
}

actor ExplosionTrap
{
    Projectile
    Speed 0
    
    RenderStyle Add
    +NOINTERACTION
    
    States
    {
      Spawn:
        BAL1 A 0 bright
        MISL B 0 bright A_PlaySound("weapons/rocklx")
        MISL B 0 bright A_Explode(128, 104, XF_HURTSOURCE | XF_NOTMISSILE)
        MISL B 4 bright ACS_NamedExecuteWithResult("BMark_MarkInRadius", 140, 128, true)
        MISL C 6 bright
        MISL D 8 bright
        stop
    }
}

actor FunhouseExplosion
{
    Projectile
    Speed 0
    DamageType "FunhouseExplosion"
    
    Scale 2.5
    RenderStyle Add
    +NOINTERACTION
    
    States
    {
      Spawn:
        BAL1 A 0 bright
        MISL B 0 bright A_PlaySound("weapons/rocklx")
        MISL B 0 bright A_Explode(75, 96, XF_HURTSOURCE | XF_NOTMISSILE, 0, 72)
        MISL B 0 bright A_RadiusThrust(9000, 96)
        MISL B 4 bright ACS_NamedExecuteWithResult("BMark_MarkInRadius", 140, 128, true)
        MISL C 6 bright
        MISL D 8 bright
        stop
    }
}

actor FunhouseWallBreak 28501
{
    //$Category "Balancemaps/MAP02"
    //$Title "Funhouse wall breaker"
    //$Sprite MISLB0
    //$NotAngled
    //$Color 7
    
    DamageFactor "None", 0
    DamageFactor "Normal", 0
    DamageFactor "FunhouseExplosion", 1
    
    +NOGRAVITY
    +DONTFALL
    +VULNERABLE
    Health 10
    
    States
    {
      Spawn:
        TNT1 A -1
        loop
      
      Death:
        TNT1 A 0 A_CallSpecial(special, args[0], args[1], args[2], args[3], args[4])
        stop
    }
}

actor GravelordSoul: LostSoul
{
    Translation "80:111=168:191", "128:143=171:191", "152:159=176:191", "48:79=168:191", "144:151=180:191", "13:15=188:191", "160:167=172:188", "9:12=188:191", "236:239=191:191", "16:47=168:191", "208:223=168:191", "112:127=172:191"
    Health 40
    RenderStyle Add
    +BRIGHT
    +LOOKALLAROUND
    +QUICKTORETALIATE
    
    Species "Funhouse"
}

actor FunhouseTimedFireball: DoomImpBall
{
    Health 35
    Damage (0)
    
    SeeSound   "imp/attack_quiet"
    DeathSound "imp/shotx_quiet"
    Species    "Funhouse"
    
    +THRUSPECIES
    +HITTRACER
    
    var int user_timer;
    
    States
    {
      Spawn: 
        BAL1 A 0 bright
        BAL1 A 0 bright A_SetUserVar("user_timer", health)
        goto SpawnLoop
      
      SpawnLoop:
        BAL1 A 0 bright A_JumpIf(user_timer < 0, "TimerDone")
        BAL1 A 1 bright A_SetUserVar("user_timer", user_timer - 1)
        BAL1 A 0 bright A_JumpIf(user_timer < 0, "TimerDone")
        BAL1 A 1 bright A_SetUserVar("user_timer", user_timer - 1)
        BAL1 A 0 bright A_JumpIf(user_timer < 0, "TimerDone")
        BAL1 A 1 bright A_SetUserVar("user_timer", user_timer - 1)
        BAL1 A 0 bright A_JumpIf(user_timer < 0, "TimerDone")
        BAL1 A 1 bright A_SetUserVar("user_timer", user_timer - 1)
        BAL1 A 0 bright A_JumpIf(user_timer < 0, "TimerDone")
        BAL1 A 1 bright A_SetUserVar("user_timer", user_timer - 1)
        BAL1 A 0 bright A_JumpIf(user_timer < 0, "TimerDone")
        BAL1 A 1 bright A_SetUserVar("user_timer", user_timer - 1)
        
        BAL1 B 0 bright A_JumpIf(user_timer < 0, "TimerDone")
        BAL1 B 1 bright A_SetUserVar("user_timer", user_timer - 1)
        BAL1 B 0 bright A_JumpIf(user_timer < 0, "TimerDone")
        BAL1 B 1 bright A_SetUserVar("user_timer", user_timer - 1)
        BAL1 B 0 bright A_JumpIf(user_timer < 0, "TimerDone")
        BAL1 B 1 bright A_SetUserVar("user_timer", user_timer - 1)
        BAL1 B 0 bright A_JumpIf(user_timer < 0, "TimerDone")
        BAL1 B 1 bright A_SetUserVar("user_timer", user_timer - 1)
        BAL1 B 0 bright A_JumpIf(user_timer < 0, "TimerDone")
        BAL1 B 1 bright A_SetUserVar("user_timer", user_timer - 1)
        BAL1 B 0 bright A_JumpIf(user_timer < 0, "TimerDone")
        BAL1 B 1 bright A_SetUserVar("user_timer", user_timer - 1)
        loop
      
      TimerDone:
        BAL1 A 0 A_Stop
        BAL1 A 0 A_Scream
        BAL1 A 0 A_ChangeFlag("MISSILE", 0)
        goto Death
    
      Death:
        "----" A 0 ACS_NamedExecuteWithResult("BMark_DealDamage", 20, AAPTR_TRACER)
        "----" A 0 ACS_NamedExecuteWithResult("BMark_MarkFromProjectile", 175, AAPTR_TRACER)
        goto DoomImpBall::Death
    }
}

actor FunhouseTimedFireball_Trap: FunhouseTimedFireball
{
    States
    {
      TimerDone:
        BAL1 A 0 A_Stop
        BAL1 A 0 A_Scream
        BAL1 A 0 A_ChangeFlag("MISSILE", 0)
        goto Death
    
      Death:
        "----" A 0 ACS_NamedExecuteWithResult("BMark_DealDamage", 20, AAPTR_TRACER, false, 200)
        "----" A 0 ACS_NamedExecuteWithResult("BMark_MarkFromProjectile", 175, AAPTR_TRACER)
        goto DoomImpBall::Death
    }
}

actor ForgeBall: DoomImpBall
{
	Damage (20)
}

actor ForgeNoKnockBack: DoomImpBall
{
	Scale 0.75
	+NODAMAGETHRUST
	Damage (10)
}

actor BaronBallNew: BaronBall
{
	+NODAMAGETHRUST
	Radius 6
	Height 6
	Damage (20)
}

actor BaronBallTrap: BaronBallNew
{
    +HITTRACER
    
    Damage 0
    
    States
    {
      Death:
        "----" A 0 ACS_NamedExecuteWithResult("BMark_DealDamage", 25, AAPTR_TRACER)
        "----" A 0 ACS_NamedExecuteWithResult("BMark_MarkFromProjectile", 175, AAPTR_TRACER)
        goto Super::Death
    }
}



actor FlamethrowerTrap: SwitchableDecoration 29552
{
    //$Category "Balancemaps/Traps"
    //$Title "Flamethrower"
    //$Sprite CFCFC0
    //$Color 12
    
    //$Arg0 "Turn speed (yaw)"
    //$Arg0Tooltip "In degrees/second."
    
    //$Arg1 "Turn speed (pitch)"
    //$Arg1Tooltip "In degrees/second."
    
    //$Arg2 "Warning puff?"
    //$Arg2Type 11
    //$Arg2Enum { 0 = "Yes"; 1 = "No"; }
    
    +NOINTERACTION
    
    Radius 3
    Height 4
    
    var int user_setup;
    var int user_angle;
    var int user_pitch;
    var int user_active;
    
    States
    {
      Spawn:
      Setup:
        TNT1 A 0
        TNT1 A 0 ACS_NamedExecuteWithResult("BMaps_TurnFlamethrower", args[0], args[1])
        TNT1 A 0 A_SetUserVar("user_setup", 1)
        goto Active
      
      Active:
        TNT1 A 0 A_JumpIf(user_setup == 0, "Setup")
        TNT1 A 0 A_JumpIf(user_active > 0, "ActiveLoop")
        TNT1 A 0 A_JumpIf(args[2] > 0, "SkipWarmup")
        TNT1 A 0 A_PlaySound("balancemaps/flamethrower_puff",     3, 0.6)
        TNT1 AA 1 A_SpawnItemEx("FLTrapFlame_Visual1", 0,0,0, 0.1 * cos(pitch), 0, 0.1 * sin(pitch), 0, SXF_TRANSFERPITCH)
        TNT1 AA 1 A_SpawnItemEx("FLTrapFlame_Visual2", 0,0,0, 0.4 * cos(pitch), 0, 0.4 * sin(pitch), 0, SXF_TRANSFERPITCH)
        TNT1 AA 1 A_SpawnItemEx("FLTrapFlame_Visual3", 0,0,0, 0.7 * cos(pitch), 0, 0.7 * sin(pitch), 0, SXF_TRANSFERPITCH)
        TNT1 AA 1 A_SpawnItemEx("FLTrapFlame_Visual4", 0,0,0, 1   * cos(pitch), 0, 1   * sin(pitch), 0, SXF_TRANSFERPITCH)
        TNT1 A 18
      
      SkipWarmup:
        TNT1 A 0 A_PlaySound("balancemaps/flamethrower_on",       3, 0.6)
        TNT1 A 0 A_PlaySound("balancemaps/flamethrower_loop",     5, 0.05, true)
        TNT1 A 0 A_PlaySound("balancemaps/flamethrower_crackle",  6, 0.4,  true)
        TNT1 A 0 A_SetUserVar("user_active", 1)
        goto ActiveLoop
      
      ActiveLoop:
        TNT1 A 6 A_CustomMissile("FLTrapFlame", 0, 0, frandom(-2, 2), CMF_AIMDIRECTION | CMF_ABSOLUTEPITCH | CMF_SAVEPITCH, pitch + frandom(-1, 1))
        loop
        
      SetupInactive:
        TNT1 A 0
        TNT1 A 0 ACS_NamedExecuteWithResult("BMaps_TurnFlamethrower", args[0], args[1])
        TNT1 A 0 A_SetUserVar("user_setup", 1)
        goto Inactive
      
      Inactive:
        TNT1 A 0 A_JumpIf(user_setup == 0, "SetupInactive")
        TNT1 A 0 A_JumpIf(user_active == 0, "AlreadyOff")
        TNT1 A 0 A_StopSound(5)
        TNT1 A 0 A_StopSound(6)
        TNT1 A 0 A_PlaySound("balancemaps/flamethrower_off", 3, 0.5)
        TNT1 A 0 A_SetUserVar("user_active", 0)
        goto AlreadyOff
      
      AlreadyOff:
        TNT1 A -1
        loop
    }
}

actor FlamethrowerTrap_Off: FlamethrowerTrap 29553
{
    //$Title "Flamethrower (off)"
    //$Sprite CFCFA0
    
    States
    {
      Spawn:
        goto SetupInactive
    }
}

actor FLTrapFlame
{
    Radius 4
    Height 4
    
    Projectile
    Speed 6
    DamageType "Fire"
    Obituary "%o got roasted."
    
    +THRUACTORS
    +NODAMAGETHRUST
    +BLOODLESSIMPACT
    +BOUNCEONWALLS
    +BOUNCEONFLOORS
    +BOUNCEONCEILINGS
    
    BounceFactor 0.5
    
    var int user_phase1;
    
    States
    {
      Spawn:
        TNT1 A 0
        TNT1 A 0 A_ChangeVelocity(0,0,frandom(-0.05, 0.125), CVF_RELATIVE)
        TNT1 A 0 A_Explode(10, 32, true, false, 24)
        TNT1 AA 1 A_SpawnItemEx("FLTrapFlame_Visual1", frandom(-6, 6), 0, 0, frandom(-1.5, 1.5),frandom(-0.2, 0.2),frandom(-0.2, 0.2), 0, 0, 128)
        TNT1 A 0 A_ChangeVelocity(0,0,frandom(-0.05, 0.125), CVF_RELATIVE)
        TNT1 A 0 A_Explode(9, 30, true, false, 22)
        TNT1 AA 1 A_SpawnItemEx("FLTrapFlame_Visual1", frandom(-6, 6), 0, 0, frandom(-1.5, 1.5),frandom(-0.2, 0.2),frandom(-0.2, 0.2), 0, 0, 128)
        TNT1 A 0 A_ChangeVelocity(0,0,frandom(-0.05, 0.125), CVF_RELATIVE)
        TNT1 A 0 A_Explode(8, 29, true, false, 20)
        TNT1 AA 1 A_SpawnItemEx("FLTrapFlame_Visual1", frandom(-6, 6), 0, 0, frandom(-1.5, 1.5),frandom(-0.2, 0.2),frandom(-0.2, 0.2), 0, 0, 128)
        TNT1 A 0 A_ChangeVelocity(0,0,frandom(-0.05, 0.125), CVF_RELATIVE)
        TNT1 A 0 A_Explode(7, 28, true, false, 20)
        TNT1 AA 1 A_SpawnItemEx("FLTrapFlame_Visual1", frandom(-6, 6), 0, 0, frandom(-1.5, 1.5),frandom(-0.2, 0.2),frandom(-0.2, 0.2), 0, 0, 128)
        
        TNT1 A 0 A_ChangeVelocity(0,0,frandom(0.125, 0.375), CVF_RELATIVE)
        TNT1 A 0 A_Explode(6, 27, true, false, 18)
        TNT1 AA 1 A_SpawnItemEx("FLTrapFlame_Visual2", frandom(-6, 6), 0, 0, frandom(-1.5, 1.5),frandom(-0.3, 0.3),frandom(-0.3, 0.3), 0, 0, 128)
        TNT1 A 0 A_ChangeVelocity(0,0,frandom(-0.05, 0.125), CVF_RELATIVE)
        TNT1 A 0 A_Explode(6, 29, true, false, 17)
        TNT1 AA 1 A_SpawnItemEx("FLTrapFlame_Visual2", frandom(-6, 6), 0, 0, frandom(-1.5, 1.5),frandom(-0.3, 0.3),frandom(-0.3, 0.3), 0, 0, 128)
        TNT1 A 0 A_ChangeVelocity(0,0,frandom(-0.05, 0.125), CVF_RELATIVE)
        TNT1 A 0 A_Explode(5, 30, true, false, 19)
        TNT1 AA 1 A_SpawnItemEx("FLTrapFlame_Visual2", frandom(-6, 6), 0, 0, frandom(-1.5, 1.5),frandom(-0.3, 0.3),frandom(-0.3, 0.3), 0, 0, 128)
        TNT1 A 0 A_ChangeVelocity(0,0,frandom(-0.05, 0.125), CVF_RELATIVE)
        TNT1 A 0 A_Explode(5, 33, true, false, 19)
        TNT1 AA 1 A_SpawnItemEx("FLTrapFlame_Visual2", frandom(-6, 6), 0, 0, frandom(-1.5, 1.5),frandom(-0.3, 0.3),frandom(-0.3, 0.3), 0, 0, 128)
        
        TNT1 A 0 A_ChangeVelocity(0,0,frandom(-0.05, 0.125), CVF_RELATIVE)
        TNT1 A 0 A_Explode(5, 36, true, false, 20)
        TNT1 AA 1 A_SpawnItemEx("FLTrapFlame_Visual3", frandom(-6, 6), 0, 0, frandom(-1.5, 1.5),frandom(-0.4, 0.4),frandom(-0.4, 0.4), 0, 0, 128)
        TNT1 A 0 A_ChangeVelocity(0,0,frandom(-0.05, 0.125), CVF_RELATIVE)
        TNT1 A 0 A_Explode(5, 42, true, false, 21)
        TNT1 AA 1 A_SpawnItemEx("FLTrapFlame_Visual3", frandom(-6, 6), 0, 0, frandom(-1.5, 1.5),frandom(-0.4, 0.4),frandom(-0.4, 0.4), 0, 0, 128)
        TNT1 A 0 A_ChangeVelocity(0,0,frandom(-0.05, 0.125), CVF_RELATIVE)
        TNT1 A 0 A_Explode(4, 49, true, false, 22)
        TNT1 AA 1 A_SpawnItemEx("FLTrapFlame_Visual3", frandom(-6, 6), 0, 0, frandom(-1.5, 1.5),frandom(-0.4, 0.4),frandom(-0.4, 0.4), 0, 0, 128)
        TNT1 A 0 A_ChangeVelocity(0,0,frandom(-0.05, 0.125), CVF_RELATIVE)
        TNT1 A 0 A_Explode(4, 49, true, false, 24)
        TNT1 AA 1 A_SpawnItemEx("FLTrapFlame_Visual3", frandom(-6, 6), 0, 0, frandom(-1.5, 1.5),frandom(-0.4, 0.4),frandom(-0.4, 0.4), 0, 0, 128)
        
        TNT1 A 0 A_ChangeVelocity(0,0,frandom(-0.05, 0.125), CVF_RELATIVE)
        TNT1 A 0 A_Explode(4, 56, true, false, 26)
        TNT1 AA 1 A_SpawnItemEx("FLTrapFlame_Visual4", frandom(-6, 6), 0, 0, frandom(-1.5, 1.5),frandom(-0.6, 0.6),frandom(-0.6, 0.6), 0, 0, 128)
        TNT1 A 0 A_ChangeVelocity(0,0,frandom(-0.05, 0.125), CVF_RELATIVE)
        TNT1 A 0 A_Explode(4, 61, true, false, 28)
        TNT1 AA 1 A_SpawnItemEx("FLTrapFlame_Visual4", frandom(-6, 6), 0, 0, frandom(-1.5, 1.5),frandom(-0.6, 0.6),frandom(-0.6, 0.6), 0, 0, 128)
        TNT1 A 0 A_ChangeVelocity(0,0,frandom(-0.05, 0.125), CVF_RELATIVE)
        TNT1 A 0 A_Explode(3, 64, true, false, 30)
        TNT1 AA 1 A_SpawnItemEx("FLTrapFlame_Visual4", frandom(-6, 6), 0, 0, frandom(-1.5, 1.5),frandom(-0.6, 0.6),frandom(-0.6, 0.6), 0, 0, 128)
        TNT1 A 0 A_ChangeVelocity(0,0,frandom(-0.05, 0.125), CVF_RELATIVE)
        TNT1 A 0 A_Explode(3, 68, true, false, 30)
        TNT1 AA 1 A_SpawnItemEx("FLTrapFlame_Visual4", frandom(-6, 6), 0, 0, frandom(-1.5, 1.5),frandom(-0.6, 0.6),frandom(-0.6, 0.6), 0, 0, 128)
        
        TNT1 A 0 A_ChangeVelocity(0,0,frandom(-0.05, 0.125), CVF_RELATIVE)
        TNT1 A 0 A_Explode(3, 70, true, false, 32)
        TNT1 AA 1 A_SpawnItemEx("FLTrapFlame_Visual5", frandom(-6, 6), 0, 0, frandom(-1.5, 1.5),frandom(-0.9, 0.9),frandom(-0.9, 0.9), 0, 0, 128)
        TNT1 A 0 A_ChangeVelocity(0,0,frandom(-0.05, 0.125), CVF_RELATIVE)
        TNT1 A 0 A_Explode(3, 74, true, false, 32)
        TNT1 AA 1 A_SpawnItemEx("FLTrapFlame_Visual5", frandom(-6, 6), 0, 0, frandom(-1.5, 1.5),frandom(-0.9, 0.9),frandom(-0.9, 0.9), 0, 0, 128)
        TNT1 A 0 A_ChangeVelocity(0,0,frandom(-0.05, 0.125), CVF_RELATIVE)
        TNT1 A 0 A_Explode(3, 77, true, false, 32)
        TNT1 AA 1 A_SpawnItemEx("FLTrapFlame_Visual5", frandom(-6, 6), 0, 0, frandom(-1.5, 1.5),frandom(-0.9, 0.9),frandom(-0.9, 0.9), 0, 0, 128)
        TNT1 A 0 A_ChangeVelocity(0,0,frandom(-0.05, 0.125), CVF_RELATIVE)
        TNT1 A 0 A_Explode(3, 80, true, false, 32)
        TNT1 AA 1 A_SpawnItemEx("FLTrapFlame_Visual5", frandom(-6, 6), 0, 0, frandom(-1.5, 1.5),frandom(-0.9, 0.9),frandom(-0.9, 0.9), 0, 0, 128)
        
        TNT1 A 0 A_ChangeVelocity(0,0,frandom(-0.05, 0.125), CVF_RELATIVE)
        TNT1 A 0 A_Explode(3, 82, true, false, 32)
        TNT1 AA 1 A_SpawnItemEx("FLTrapFlame_Visual6", frandom(-6, 6), 0, 0, frandom(-1.5, 1.5),frandom(-1.4, 1.4),frandom(-1.4, 1.4), 0, 0, 128)
        TNT1 A 0 A_ChangeVelocity(0,0,frandom(-0.05, 0.125), CVF_RELATIVE)
        TNT1 A 0 A_Explode(2, 86, true, false, 32)
        TNT1 AA 1 A_SpawnItemEx("FLTrapFlame_Visual6", frandom(-6, 6), 0, 0, frandom(-1.5, 1.5),frandom(-1.4, 1.4),frandom(-1.4, 1.4), 0, 0, 128)
        TNT1 A 0 A_ChangeVelocity(0,0,frandom(-0.05, 0.125), CVF_RELATIVE)
        TNT1 A 0 A_Explode(2, 90, true, false, 32)
        TNT1 AA 1 A_SpawnItemEx("FLTrapFlame_Visual6", frandom(-6, 6), 0, 0, frandom(-1.5, 1.5),frandom(-1.4, 1.4),frandom(-1.4, 1.4), 0, 0, 128)
        TNT1 A 0 A_ChangeVelocity(0,0,frandom(-0.05, 0.125), CVF_RELATIVE)
        TNT1 A 0 A_Explode(2, 96, true, false, 32)
        TNT1 AA 1 A_SpawnItemEx("FLTrapFlame_Visual6", frandom(-6, 6), 0, 0, frandom(-1.5, 1.5),frandom(-1.4, 1.4),frandom(-1.4, 1.4), 0, 0, 128)
        stop
    }
}

actor FLTrapFlame_Visual1
{
    +NOINTERACTION
    +CLIENTSIDEONLY
    +FORCEXYBILLBOARD
    
    RenderStyle Add
    Scale 0.8
    Alpha 0.6
    
    States
    {
      Spawn:
        CFCF ABCDEFGHIJKLMNOP 2 bright A_ScaleVelocity(frandom(0.85, 0.95))
        stop
    }
}

actor FLTrapFlame_Visual2: FLTrapFlame_Visual1 { Scale 0.9  Alpha 0.45 }
actor FLTrapFlame_Visual3: FLTrapFlame_Visual1 { Scale 1.0  Alpha 0.3  }
actor FLTrapFlame_Visual4: FLTrapFlame_Visual1 { Scale 1.15 Alpha 0.25 }
actor FLTrapFlame_Visual5: FLTrapFlame_Visual1 { Scale 1.35 Alpha 0.2  }
actor FLTrapFlame_Visual6: FLTrapFlame_Visual1 { Scale 1.7  Alpha 0.15 }