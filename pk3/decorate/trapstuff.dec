actor TinierRocket: Rocket
{
    Radius 4
}


actor CacodemonBall_Trap: CacodemonBall
{
    +HITTRACER
    
    States
    {
      Death:
        "----" A 0 ACS_NamedExecuteWithResult("BMark_MarkFromProjectile", 175, AAPTR_TRACER)
        goto Super::Death
    }
}

actor GravelordRevenant: Revenant
{
    Translation "80:111=168:191", "128:143=171:191", "152:159=176:191", "48:79=168:191", "144:151=180:191", "13:15=188:191", "160:167=172:188", "9:12=188:191", "236:239=191:191", "16:47=168:191", "208:223=168:191", "112:127=172:191"
    RenderStyle Add
    
    Health 600
    MinMissileChance 0
    PainChance 50
    +BRIGHT
    +LOOKALLAROUND
    +QUICKTORETALIATE
    
    States
    {
      Missile:
        SKEL J 0 Bright A_FaceTarget
        SKEL J 10 Bright A_FaceTarget
        SKEL K 10 A_CustomMissile("GravelordRevenantTracer", 40)
        SKEL K 10 A_FaceTarget
        goto See
        
      Melee:
        SKEL G 0 A_FaceTarget
        SKEL G 4 A_SkelWhoosh
        SKEL H 4 A_FaceTarget
        SKEL I 4 A_SkelFist
        goto See
        
      Death:
        SKEL LM 7
        SKEL N 7 A_Scream
        SKEL O 7 A_NoBlocking
        SKEL P 7
        SKEL QQQQQQQQQQQQQQQQQQQQQQQQQ 1 A_FadeOut(0.04, 0)
        TNT1 A 140
        stop
    }
}

actor GravelordRevenantTracer: RevenantTracer
{
    +HITMASTER
    
    States
    {
      Death:
        "----" A 0 ACS_NamedExecuteWithResult("BMark_MarkFromProjectile", 175, AAPTR_MASTER)
        goto Super::Death
    }
}

actor ExplosionTrap
{
    Projectile
    Speed 0
    
    RenderStyle Add
    
    States
    {
      Spawn:
        TNT1 A 0
        MISL B 0 bright A_PlaySound("weapons/rocklx")
        MISL B 0 bright A_Explode(128, 128)
        MISL B 4 bright ACS_NamedExecuteWithResult("BMark_MarkInRadius", 105, 128, true)
        MISL C 6 bright
        MISL D 8 bright
        stop
    }
}