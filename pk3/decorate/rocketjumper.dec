

actor RJInvulnerability: PowerInvulnerable
{
    Powerup.Duration 0x7FFFFFFF
    Powerup.Color    "none"
}

actor RJUnshootable:     Boolean {}

actor RJMakeUnshootable: Trigger
{
    States
    {
      Pickup:
        TNT1 A 0 A_ChangeFlag("SHOOTABLE", false)
        TNT1 A 0 A_GiveInventory("RJUnshootable")
        stop
    }
}

actor RJMakeShootable: Trigger
{
    States
    {
      Pickup:
        TNT1 A 0 A_ChangeFlag("SHOOTABLE", true)
        TNT1 A 0 A_TakeInventory("RJUnshootable")
        stop
    }
}


actor InfinityRocketJumper: Weapon 28501
{
    Weapon.SelectionOrder 2500
    Weapon.SlotNumber 5
    
    +UNTOSSABLE
    
    Inventory.PickupMessage "You got the infinity rocket jumper!"
    Tag "Infinity Rocket Jumper"
    
    States
    {
      Ready:
        MISG A 1 A_WeaponReady
        loop
        
      Deselect:
        MISG A 1 A_Lower
        loop
        
      Select:
        MISG A 1 A_Raise
        loop
        
      Fire:
        MISG B 8  A_GunFlash
        MISG B 12 A_FireCustomMissile("RJRocket")
        MISG B 0  A_ReFire
        goto Ready
        
      AltFire:
        MISG B 0  A_GunFlash
        MISG B 20 A_FireCustomMissile("RJRocket")
        MISG B 0  A_ReFire
        goto Ready
        
      Flash:
        MISF A  3 Bright A_Light1
        MISF B  4 Bright
        MISF CD 4 Bright A_Light2
        goto LightDone
        
      AltFlash:
        MISF A  2 Bright A_Light1
        MISF B  3 Bright
        MISF C  4 Bright A_Light2
        MISF D  6 Bright A_Light1
        goto LightDone
        
      Spawn:
        LAUN A -1
        stop
    }
}

actor RocketJumper: InfinityRocketJumper 28502
{
    Weapon.AmmoType1 "RocketAmmo"
    Weapon.AmmoType2 "RocketAmmo"
    Weapon.AmmoUse1 1
    Weapon.AmmoUse2 1
    Weapon.AmmoGive 2
    
    Inventory.PickupMessage "You got the rocket jumper!"
    Tag "Rocket Jumper"
}

actor RJRocket
{
    Radius 11
    Height 8

    Projectile
    Speed 20
    Damage 0

    +RANDOMIZE
    +DEHEXPLOSION
    +ROCKETTRAIL
    +MTHRUSPECIES

    SeeSound "weapons/rocklf"
    DeathSound "weapons/rocklx"

    Obituary "%o got jumped over by %k."
      
    States
    {
      Spawn:
        MISL A -1 bright
        loop
        
      Death:
        MISL B 0 ACS_NamedExecuteWithResult("BMaps_RocketJumpOn")
        MISL B 0 A_Explode(128, 128)
        MISL B 0 ACS_NamedExecuteWithResult("BMaps_RocketJumpOff")
        MISL B 8 bright
        MISL C 6 bright
        MISL D 4 bright
        stop
    }
}